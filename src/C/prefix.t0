{ | { | key 10 != } { | } while } :!//
// Now we have C++-style comments

// //////////////////////////////////////////////////////////////////////////
// Prefix.t0
// T0 Language Features written in T0
// //////////////////////////////////////////////////////////////////////////

{ c b | { | c () ! } b while } ::until

{ let 0 :prev 0 :c | { | prev 42 =  c 47 =  & } { | c :prev key :c } until } :!/*
/*
 *  Now we have C-style comments
 */

 { let 0 :prev 0 :c | { | prev 42 =  c 41 =  & } { | c :prev key :c } until } :!(*
 (*
  *  Now we have Beta-style comments, let's just use to comment out code
  *)

1 1 = :true         // define true
1 2 = :false        // define false
{ } :nil            // define 'nil', like doing nil = new Object() in Java/JS
nil :nop
{ n | 0 n - } ::neg // negate

// Standard Forth-like Stack Functions
// { _ } ::drop // coded natively
{ _ __ } ::drop2
{ a | a a } ::dup
{ a b | b a } ::swap

9  charCode :tab { | tab .$ } ::.tab
10 charCode :nl  { | nl  .$ } ::.nl
13 charCode :cr

{ start end block | { | start end <= } { | start block () start++ } while } ::for

// Standard High-Order Functions
{ a f | 0 a # 1 - { i | a i @ f () } for } ::forEach // ??? rename select?
{ a f | [ a f forEach ] } ::map
{ a v f | v a f forEach } ::reduce
{ a p | [ a { e | e p () { | e } if } forEach ] } ::filter
// { a | a " " { | + } reduce } ::join // TODO: need $+ or concat

/*
TODO: move to own file
{ block |
  now { start |
    block ()
    now start - .
  } ()
} ::bench
*/

{ | key } ::_key_

{ s let &_key_ :oldKey 0 :i |
  { let s len :l |
    { |
      i l <
        { | s i charAt i++ }
        { | oldKey ::_key_ -1 }
      ifelse
    } ::_key_
  } ()
  eval_
} ::eval


{ a b | [ a { } forEach b { } forEach ] } ::+[]
{ a | [ a { } forEach ] } ::clone[]

// TODO: Move to Own File
{ n | n 1 <= { | 1 } { | n 1 - fact n * } ifelse } ::fact

// P(n,r) = n!/(n - r)!
{ n r | n fact n r - fact / } ::P

// C(n,r) = n!/[r!(n - r)!]
{ n r | n r P r fact / } ::C
