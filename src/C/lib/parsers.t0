// parsers.t0
// A Parse Combinator Library

// A Parser Stream - used as input for parsers

// TODO: Add .value$

{ str pos value ignore let false :tail |
  { m |
    m switch
      'parse  { parser this | this parser () }
      'parseToken { parser this |
        ignore
          {
            let this .ignoreOff parser () :result |
            result
              { | ignore result .ignoreOn }
              { | result }
            ifelse
          }
          { | this parser () }
        ifelse
      }
      'ignoreOff { this | str pos value false PStream }
      'ignoreOn { ignore this | str pos value ignore PStream .maybeIgnore }
      'pos    { this | pos }
      'head   { this |
        // [ " pos: " pos " , head-> " str pos charAt ] join .$ .nl
        str pos charAt }
      'tail   { this |
        tail !
          { | str pos 1 + this .head ignore PStream ignore { | .maybeIgnore } if :tail }
        if
        tail
      }
      'maybeIgnore { this let this ignore () :ps |
        ps
          { | this .value ps .:value  }
          { | this }
        ifelse
      }
      'value    { this | value }
      ':xxvalue { v2 this | value v2 = { | this } { | str pos v2 ignore PStream } ifelse }
      ':value   { v2 this | str pos v2 ignore PStream }
      'toString { this | [ " PStream pos: " pos " , value: '" value '' nl ] join }
                { this | [ " PStream Unknown Method '" m '' nl ] join dup .$ }
    end
  }
} ::PStream

{ p | { ps | p ps .parseToken } } ::tok

// ???: add do$ ?, rename charAt c@?
{ str v | { ps let 0 :i |
  { | i str len <  { | ps .head str i charAt = } && } { | ps .tail :ps  i++ } while
  str len i = { | v ps .:value } { | false } ifelse
} /* tok */ } ::litMap

{ str | str str litMap } ::lit

{ start end c | c start >=  c end <= & } ::inRange
{ start end | { ps |
  start end ps .head inRange { | ps .tail } { | false } ifelse
} } ::range

{ parsers | parsers { p | p fn? { | p } { | p lit } ifelse } map } ::prepare

{ parsers | parsers prepare :parsers { ps let 0 :i |
  [ { | i parsers # < { | parsers i @ ps .parse :ps ps } && } { | i++ ps .value } while ]
  parsers # i = { a | a ps .:value } { _ | false } ifelse
} } ::seq

{ parsers i | parsers seq { a | a i @ } mapp } ::seq1

{ parsers | parsers prepare :parsers { ps let 0 :i false :ret |
  { | i parsers # < { | parsers i @ ps .parse :ret ret ! } && } { | i++ } while
  ret
} } ::alt

{ parser min | { ps let 0 :i false :ret |
  [ { | ps :ret  parser ps .parse :ps ps } { | i . .nl i++ ps .value } while ]
  i min >=  { | ret .:value } { _ | #F } ifelse
} } ::repeatp

{ | 0 repeatp } ::star
{ | 1 repeatp } ::plus

{ parser delim |
  [ [ parser delim ] 0 seq1 star (* parser opt *) ] seq
//  { a | [ a 0 @  { e | e } do a 1 @ ] } mapp
} ::delim

{ parser | { ps let parser ps .parse :ret | ret { | ret } { | false ps .:value } ifelse } } ::opt

{ parser | { ps | parser ps .parse { | false } { | ps } ifelse } } ::notp

{ str | { ps | str ps .head charCode $indexOf -1 = { | ps .tail .value charCode  ps .tail .:value } { | false } ifelse } } ::notChars

{ str | { ps | str ps .head charCode $indexOf -1 > { | ps .tail .value charCode  ps .tail .:value } { | false } ifelse } } ::anyChar

{ p f | { ps | p ps .parse :ps ps { | ps .value f () ps .:value } { | false } ifelse } } ::mapp

{ o m super f | { ps | o m super () () f mapp ps .parse } } ::action
